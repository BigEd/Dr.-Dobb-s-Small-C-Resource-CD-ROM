COLD

HEX

( SIMPLE S-RECORD LOADER FOR Max-FORTH V3.5
( DOES NOT CHECK CHECKSUM
(
( EDIT TO CHANGE 
( LOAD-OFFSET INITILIZATION
( IF OFFSET IS NEEDED
( OTHERWISE LOAD WILL BEGIN
( WITH 0 OFFSET

( VER 1.1 7/9/93, CHANGED EOF PRINTOUT

53 CONSTANT "S"
VARIABLE REC-S
VARIABLE REC-TYPE
VARIABLE REC-LENGTH
VARIABLE REC-LOAD-ADD
VARIABLE REC-CHKSUM
VARIABLE LOAD-OFFSET

0000 LOAD-OFFSET ! ( CAN CHANGE OFFSET IF LOAD ADDR IS NOT RUN ADDR

: GETC
  >IN @ TIB @ + C@ 1 >IN +!
;

: 1#
  GETC 30 - DUP 9 > IF 7 - THEN 0 MAX F MIN
;

: 2#
  1# 10 *
  1# +
;

: 4#
  1# 10 *
  1# + 10 *
  1# + 10 *
  1# +
;

: SREC
  CR ." READY TO LOAD, OFFSET = " LOAD-OFFSET @ . CR
  BEGIN
    QUERY
    GETC   REC-S !
    1#     REC-TYPE !
    2# 3 - REC-LENGTH !
    4#     LOAD-OFFSET @ + REC-LOAD-ADD !
    REC-S @ "S" =
    IF
      REC-TYPE @ 1 =
      IF
        REC-LOAD-ADD @ U.
        CR
        BEGIN
          REC-LENGTH @
        WHILE
          2# REC-LOAD-ADD @ C!
          1 REC-LOAD-ADD +!
          -1 REC-LENGTH +!
        REPEAT
      THEN
    THEN
    REC-TYPE @ 9 =
  UNTIL
  0 #TIB !
;

SREC

